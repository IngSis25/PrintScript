plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'java-library'
    id 'org.jlleitschuh.gradle.ktlint'
    id 'maven-publish'
}

group = 'io.printscript'
version = '0.1.6-SNAPSHOT'

repositories {
    mavenCentral()
}

plugins.withId('java-library') {
    dependencies {
        constraints {
            implementation 'org.apache.commons:commons-text:1.13.0'
        }
    }
}

test {
    useJUnitPlatform()
}

kotlin {
    jvmToolchain(17)
}

/** Publicación segura para TODOS los módulos que apliquen este plugin */
publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/IngSis25/PrintScript")
            credentials {
                username = findProperty('gpr.user') ?: System.getenv('GITHUB_ACTOR')
                password = findProperty('gpr.key')  ?: System.getenv('GITHUB_TOKEN')
            }
        }
    }
    publications {
    }
}
afterEvaluate {
    if (components.findByName("java") != null) {
        publishing {
            publications {
                gpr(MavenPublication) {
                    from components.java
                    artifactId = project.name
                }
            }
        }
    }
}

tasks.register('installGitHooks', Copy) {
    from("$rootDir/hooks/pre-commit")
    into("$rootDir/.git/hooks")
}
tasks.named('build') {
    dependsOn 'installGitHooks'
}
